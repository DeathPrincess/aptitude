from aptitude_build import CString
from aptitude_build import ShellEscape
import os.path

Import('base_env')
Import('cppunit_tests_env')
Import('boost_tests_env')
Import('gtest_tests_env')
base_env.Dist('Makefile.am', 'SConscript')

cppunit_test_sources = [
    'cppunit_test_main.cc',
    'test_choice.cc',
    'test_choice_set.cc',
    'test_config_pusher.cc',
    'test_dense_setset.cc',
    'test_incremental_expression.cc',
    'test_matching.cc',
    'test_misc.cc',
    'test_parsers.cc',
    'test_promotion_set.cc',
    'test_resolver.cc',
    'test_resolver_costs.cc',
    'test_resolver_hints.cc',
    'test_setset.cc',
    'test_tags.cc',
    'test_temp.cc',
    'test_wtree.cc',
    ]

cppunit_test_extra_deps = [
    '../src/generic/apt/apt.o',
    '../src/generic/apt/aptcache.o',
    '../src/generic/apt/aptitude_resolver.o',
    '../src/generic/apt/aptitude_resolver_cost_settings.o',
    '../src/generic/apt/aptitude_resolver_cost_syntax.o',
    '../src/generic/apt/aptitude_resolver_cost_types.o',
    '../src/generic/apt/aptitude_resolver_universe.o',
    '../src/generic/apt/aptitudepolicy.o',
    '../src/generic/apt/config_signal.o',
    '../src/generic/apt/download_queue.o',
    '../src/generic/apt/dump_packages.o',
    '../src/generic/apt/globals.o',
    '../src/generic/apt/matching/compare_patterns.o',
    '../src/generic/apt/matching/match.o',
    '../src/generic/apt/matching/parse.o',
    '../src/generic/apt/matching/pattern.o',
    '../src/generic/apt/matching/serialize.o',
    '../src/generic/apt/pkg_hier.o',
    '../src/generic/apt/resolver_manager.o',
    '../src/generic/apt/tags.o',
    '../src/generic/apt/tasks.o',
    '../src/generic/problemresolver/cost.o',
    '../src/generic/problemresolver/cost_limits.o',
    '../src/generic/problemresolver/dummy_universe.o',
    '../src/generic/problemresolver/incremental_expression.o',
    '../src/generic/util/file_cache.o',
    '../src/generic/util/logging.o',
    '../src/generic/util/refcounted_base.o',
    '../src/generic/util/sqlite.o',
    '../src/generic/util/temp.o',
    '../src/generic/util/undo.o',
    '../src/generic/util/util.o',
    '../src/loggers.o',
    ]

boost_test_sources = [
    'boost_test_main.cc',
    'test_dynamic_list.cc',
    'test_dynamic_set.cc',
    'test_enumerator.cc',
    'test_file_cache.cc',
    'test_search_input_controller.cc',
    'test_sqlite.cc'
    ]

boost_test_extra_deps = [
    '../src/generic/apt/globals.o',
    '../src/generic/apt/matching/compare_patterns.o',
    '../src/generic/apt/matching/parse.o',
    '../src/generic/apt/matching/pattern.o',
    '../src/gtk/controllers/search_input.o',
    '../src/gtk/views/search_input.o',
    '../src/generic/util/file_cache.o',
    '../src/generic/util/logging.o',
    '../src/generic/util/refcounted_base.o',
    '../src/generic/util/sqlite.o',
    '../src/generic/util/temp.o',
    '../src/generic/util/util.o',
    '../src/loggers.o',
    ]

gtest_test_sources = [
    'gtest_test_main.cc',
    'test_cmdline_download_progress_display.cc',
    'test_cmdline_progress_display.cc',
    'test_cmdline_search_progress.cc',
    'test_logging.cc',
    'test_teletype_mock.cc',
    'test_terminal_mock.cc',
    'test_transient_message.o',
    ]

gtest_test_extra_deps = [
    '../src/cmdline/cmdline_download_progress_display.o',
    '../src/cmdline/cmdline_progress_display.o',
    '../src/cmdline/cmdline_progress_throttle.o',
    '../src/cmdline/cmdline_search_progress.o',
    '../src/cmdline/mocks/teletype.o',
    '../src/cmdline/mocks/terminal.o',
    '../src/cmdline/terminal.o',
    '../src/cmdline/transient_message.o',
    '../src/generic/apt/globals.o',
    '../src/generic/util/logging.o',
    '../src/generic/util/progress_info.o',
    '../src/generic/util/util.o',
    '../src/generic/views/download_progress.o',
    '../src/loggers.o',
    ]

test_data_files = [
    'file_caches/ver2_cache.db',
    'file_caches/ver3_cache.db',
    ]

boost_tests_env.Dist(boost_test_sources,
                     cppunit_test_sources,
                     gtest_test_sources,
                     test_data_files)

cppunit_test = cppunit_tests_env.Test('cppunit_test',
                                      (cppunit_test_sources,
                                       cppunit_test_extra_deps))

# The file cache test uses this to find its input data.  It must be
# the path of the parent of the file_caches directory, relative to the
# path of the test executable.  Since those are the same directory,
# "." is correct.
boost_tests_env.Append(CPPDEFINES = ('SRCDIR', ShellEscape(CString('.'))))

boost_test = boost_tests_env.Test('boost_test',
                                  (boost_test_sources,
                                   boost_test_extra_deps))

gtest_test = gtest_tests_env.Test('gtest_test',
                                  (gtest_test_sources,
                                   gtest_test_extra_deps))

# Ensure that the test file caches are copied to the build directory.
for test_input in test_data_files:
    test_file = boost_tests_env.Command(test_input,
                                        os.path.join('#', 'tests', test_input),
                                        Copy('$TARGET', '$SOURCE'))
    boost_tests_env.Requires(boost_test, test_file)

