from aptitude_build import NonHeaders

Import('programs_env')


toplevel_srcs = [
    'apt_config_treeitems.cc',
    'apt_config_treeitems.h',
    'apt_config_widgets.cc',
    'apt_config_widgets.h',
    'apt_info_tree.cc',
    'apt_info_tree.h',
    'apt_options.cc',
    'apt_options.h',
    'apt_undo_tree.cc',
    'apt_undo_tree.h',
    'aptitude.h',
    'broken_indicator.cc',
    'broken_indicator.h',
    'defaults.cc',
    'defaults.h',
    'dep_item.cc',
    'dep_item.h',
    'desc_render.cc',
    'desc_render.h',
    'download_item.cc',
    'download_item.h',
    'download_list.cc',
    'download_list.h',
    'download_thread.cc',
    'download_thread.h',
    'edit_pkg_hier.cc',
    'edit_pkg_hier.h',
    'load_config.cc',
    'load_config.h',
    'load_grouppolicy.cc',
    'load_grouppolicy.h',
    'load_pkgview.cc',
    'load_pkgview.h',
    'load_sortpolicy.cc',
    'load_sortpolicy.h',
    'loggers.cc',
    'loggers.h',
    'main.cc',
    'menu_redirect.cc',
    'menu_redirect.h',
    'menu_text_layout.cc',
    'menu_text_layout.h',
    'menu_tree.cc',
    'menu_tree.h',
    'pkg_columnizer.cc',
    'pkg_columnizer.h',
    'pkg_grouppolicy.cc',
    'pkg_grouppolicy.h',
    'pkg_info_screen.cc',
    'pkg_info_screen.h',
    'pkg_item.cc',
    'pkg_item.h',
    'pkg_item_with_subtree.h',
    'pkg_node.cc',
    'pkg_node.h',
    'pkg_sortpolicy.cc',
    'pkg_sortpolicy.h',
    'pkg_subtree.cc',
    'pkg_subtree.h',
    'pkg_tree.cc',
    'pkg_tree.h',
    'pkg_ver_item.cc',
    'pkg_ver_item.h',
    'pkg_view.cc',
    'pkg_view.h',
    'progress.cc',
    'progress.h',
    'reason_fragment.cc',
    'reason_fragment.h',
    'safe_slot_event.h',
    'solution_dialog.cc',
    'solution_dialog.h',
    'solution_fragment.cc',
    'solution_fragment.h',
    'solution_item.cc',
    'solution_item.h',
    'solution_screen.cc',
    'solution_screen.h',
    'trust.cc',
    'trust.h',
    'ui.cc',
    'ui.h',
    'ui_download_manager.cc',
    'ui_download_manager.h',
    'view_changelog.cc',
    'view_changelog.h',
    ]

sub_srcs = SConscript(dirs = [
        'cmdline',
        'generic',
        'mine',
        ])

# Note: always load the GTK+ sources (for dist, etc) even if we aren't
# building that variant.
gtk_srcs = SConscript(dirs = [
    'gtk'
    ])

# Distribute the build files.
programs_env.Dist('SConscript', 'Makefile.am')

# Note: always distribute all the source, even if it's not being
# built.
programs_env.Dist(toplevel_srcs, gtk_srcs, sub_srcs)

if programs_env.IsVariantActive('gtk'):
    aptitude_srcs = Flatten([toplevel_srcs, gtk_srcs, sub_srcs])
else:
    aptitude_srcs = Flatten([toplevel_srcs, sub_srcs])

aptitude = programs_env.Program(target = 'aptitude', source = NonHeaders(aptitude_srcs))

if programs_env.IsVariantActive('gtk'):
    # Force the .glade file to be copied to the build directory.  Note
    # the use of Requires to ensure that the glade is available for
    # aptitude, without forcing aptitude to be relinked because it
    # changed.
    aptitude_glade = programs_env.Command('gtk/aptitude.glade',
                                          '#/src/gtk/aptitude.glade',
                                          Copy('$TARGET', '$SOURCE'))
    programs_env.Requires(aptitude, aptitude_glade)

programs_env.Alias('programs', aptitude)

Return('aptitude')
